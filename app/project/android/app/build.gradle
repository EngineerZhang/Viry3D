apply plugin: 'com.android.application'

def ndkDir
if (project.rootProject.file('local.properties').exists()) {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    ndkDir = properties.getProperty('ndk.dir')
}
if (!ndkDir) {
    ndkDir = System.getenv("ANDROID_NDK_HOME")
}

if(!ndkDir || ndkDir.empty) {
    throw new GradleException('Environment Variable ANDROID_NDK_HOME for NDK path need to be setup')
}

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.1"

    defaultConfig {
        applicationId "com.viry3d.app"
        minSdkVersion 18
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"

        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86'
        }

        externalNativeBuild {
            cmake {
                arguments '-DANDROID_PLATFORM=android-18', '-DANDROID_TOOLCHAIN=clang', '-DANDROID_STL=gnustl_static'
            }
        }
    }

    externalNativeBuild {
        cmake {
            path '../CMakeLists.txt'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    exec {
        workingDir '../../../..'
        def cmd = [
                'xcopy', 'app\\bin\\Assets', 'app\\project\\android\\app\\src\\main\\assets\\Assets\\', '/E', '/Y'
        ]
        commandLine cmd
    }

    // for vulkan validation layer libs
    sourceSets {
        main {
            jniLibs {
                srcDir "${ndkDir}/sources/third_party/vulkan/src/build-android/jniLibs"
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
}
